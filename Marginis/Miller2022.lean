/-
Copyright (c) 2024 BjÃ¸rn Kjos-Hanssen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: BjÃ¸rn Kjos-Hanssen
-/
import Mathlib.Computability.PartrecCode
import Mathlib.Computability.Halting
import Mathlib.Computability.Primrec
import Mathlib.Data.Fintype.Pi
import Mathlib.Tactic.Linarith.Frontend
import Mathlib.Computability.NFA
import Mathlib.MeasureTheory.Constructions.Prod.Basic
import Mathlib.Data.Matrix.Basic
import Mathlib.Data.Nat.Digits

/-!

# Effectivizing Lusinâ€™s Theorem

[RUSSELL MILLER](http://logicandanalysis.org/index.php/jla/article/view/382)

The paper discusses Turing degrees among other things.
Here we formalize Turing reducibility (Degrees of unsolvability).

(Mathlib has a `reduce.lean` file that can be reconciled with this.)

This file introduces many-one reducibility (mapping reducibility) and
proves its basic properties.

We work with a couple of classes of functions:
 - mon (which includes both ğ““â‚ and ğ““â‚˜ and any monoid of functions)
 - monâ‚ (which fits ğ““â‚ and ğ““â‚˜ but not as general as monâ‚)
 - monâ‚˜ (which includes ğ““â‚˜ but not ğ““â‚).

We show over monâ‚ that the degrees are not rigid, using complementation.

Over monâ‚˜ we show that the degrees form an upper semilattice and has
an automorphism that simply swaps âŠ¥ := âŸ¦âˆ…âŸ§â‚˜ and âŠ¤ := âŸ¦â„•âŸ§â‚˜.

The halting problem `K` is defined in this context and
its basic degree-theoretic properties established.
-/


/-

## m-reducibility

The basic definitions at the level of sets.

-/

/-- An arbitrary monoid. -/
structure mon where
  /-- The functions under consideration (computable, primitive recursive, hyperarithmetic, etc.) -/
  func : (â„• â†’ â„•) â†’ Prop
  id : func id
  comp : âˆ€ {f g}, func f â†’ func g â†’ func (f âˆ˜ g)

/-- The unit monoid consists of `id` only. The corresponding degree structure
has equality as its equivalence.
 -/
def unitMon : mon := {
  func := fun f => f = id
  id := rfl
  comp := fun hf hg => by simp_all}

/-- Embedding on the left over â„•. -/
def inlFun : â„• â†’ â„• := fun k => 2 * k

/-- Embedding on the right over â„•. -/
def inrFun : â„• â†’ â„• := fun k => 2 * k + 1

/-- A monoid in which we can prove âŠ• is an upper bound, even if not the least one. -/
structure monâ‚ extends mon where
  inl : func inlFun
  inr : func inrFun

/-- The injective functions ca be used in defining 1-degrees, ğ““â‚. -/
def injClone : monâ‚ := {
  func := Function.Injective
  id := fun â¦ƒaâ‚ aâ‚‚â¦„ a â†¦ a
  comp := Function.Injective.comp
  inl := mul_right_injectiveâ‚€ <| Ne.symm (Nat.zero_ne_add_one 1)
  inr := Function.Injective.comp Nat.succ_injective <|mul_right_injectiveâ‚€ (by simp)
}


/-- Mapping (many-one) reducibility. -/
def m_reducible {m : mon}  (A B : â„• â†’ Bool) := âˆƒ f : â„• â†’ â„•, m.func f âˆ§ âˆ€ x, A x = B (f x)

instance (u : â„• â†’ â„•) (n : â„•):
  Fintype ((Fin (u n) â†’ Bool)) := Pi.fintype


instance (u : â„• â†’ â„•) (n : â„•):
  Fintype ((Fin (u n) â†’ Bool) â†’ Bool) := Pi.fintype

instance (n : â„•):
  Primcodable ((Fin n â†’ Bool)) := Primcodable.finArrow

instance (u : â„• â†’ â„•) (n : â„•):
  Primcodable ((Fin (u n) â†’ Bool)) := Primcodable.finArrow

instance (k : â„•) :
  Primcodable ((Fin k.succ â†’ Bool)) := Primcodable.finArrow


-- instance (n : â„•):
--   Primcodable ((Fin n â†’ Bool) â†’ Bool) := sorry


-- instance (u : â„• â†’ â„•) (n : â„•):
--   Primcodable ((Fin (u n) â†’ Bool) â†’ Bool) := by sorry

instance (u : â„• â†’ â„•) :  Denumerable ((n : â„•) Ã— (Fin (u n) â†’ Bool)) := by
  exact Denumerable.ofEncodableOfInfinite ((n : â„•) Ã— (Fin (u n) â†’ Bool))



instance : Primcodable ((Fin 2 â†’ Bool)) := Primcodable.finArrow

-- instance : Encodable (Bool â†’ Bool) := Encodable.fintypeArrowOfEncodable

instance : Encodable (Bool â†’ Bool) := by exact Encodable.fintypeArrowOfEncodable
instance : Encodable (List Bool) := by exact List.encodable

instance : Encodable (List (Fin 2)) := by exact List.encodable

def turing_functional' (f : List Bool Ã— â„• â†’ Part Bool) : Prop :=
  @Partrec (List Bool Ã— â„•) Bool _ _ f âˆ§ âˆ€ u v, u <+: v â†’
    (f (v, 0)) â‰  Part.none â†’ f (u, 0) â‰  Part.none

def turing_functional (f : List Bool â†’ â„• â†’ Part Bool) : Prop := by
  let W := @Partrecâ‚‚ (List Bool) â„• Bool _ _ _ f
  exact W âˆ§ âˆ€ n u v, u <+: v â†’
    (f u n) â‰  Part.none â†’ f v n = f v n

def turing_reducible (A B : â„• â†’ Bool) : Prop :=
  âˆƒ Ï†, turing_functional Ï† âˆ§ âˆ€ n, âˆƒ k, Ï† (List.ofFn (fun a : Fin k => B a)) n = A n

def get_part (Ïƒ : List Bool) (k : â„•) : Part Bool := Ïƒ.get? k

lemma for_refl : Partrecâ‚‚ get_part := by
  apply Computable.ofOption
  apply Computable.list_get?

theorem t_refl : Reflexive turing_reducible := by
  intro A
  use fun Ïƒ k => Ïƒ.get? k
  constructor
  constructor
  Â· exact for_refl
  Â· simp
  Â· intro n
    use n.succ
    simp
    have : âˆ€ k, (A 0 :: List.ofFn fun i : Fin k â†¦ A (i.1 + 1))
                      = List.ofFn fun i : Fin k.succ â†¦ A i := by
      intro k
      ext l
      simp
    simp_rw [this n]
    apply List.getElem_ofFn
open Classical



def getlist' (Î² : â„• â†’ Part Bool) (l : â„•)
  (h : âˆ€ k : Fin l, Î² k.1 â‰  Part.none) : List Bool := by
  exact List.ofFn (fun k : Fin l => by
    exact (Î² k.1).get (by
      have : âˆƒ q, (Î² k.1) = Part.some q := by exact Part.ne_none_iff.mp (h k)
      simp_all only [ne_eq, Bool.exists_bool]
      cases this with
      | inl h_1 => simp_all only [Part.some_dom]
      | inr h_2 => simp_all only [Part.some_dom]
    )
  )



open Nat
inductive Partrec_in {A : â„• â†’. â„•} : (â„• â†’. â„•) â†’ Prop
  | self : Partrec_in A
  | zero : Partrec_in (pure 0)
  | succ : Partrec_in succ
  | left : Partrec_in fun n : â„• => n.unpair.1
  | right : Partrec_in fun n : â„• => n.unpair.2
  | pair {f g} : Partrec_in f â†’ Partrec_in g â†’ Partrec_in fun n => pair <$> f n <*> g n
  | comp {f g} : Partrec_in f â†’ Partrec_in g â†’ Partrec_in fun n => g n >>= f
  | prec {f g} : Partrec_in f â†’ Partrec_in g â†’ Partrec_in (unpaired fun a n =>
      n.rec (f a) fun y IH => do let i â† IH; g (pair a (pair y i)))
  | rfind {f} : Partrec_in f â†’ Partrec_in fun a => rfind fun n => (fun m => m = 0) <$> f (pair a n)

def Computable_in (f g : â„• â†’ â„•) :=
  @Partrec_in (g) f

def T_reducible (A B : â„• â†’ Bool) :=
  Computable_in (fun k => (A k).toNat) (fun k => (B k).toNat)


infix:50 " â‰¤â‚œ " => (fun A B => @Partrec_in B A)

theorem computable_in_refl : Reflexive Computable_in := by
  intro A
  exact Partrec_in.self

open Partrec_in
-- (h : @Partrec_in A f) not assumed explicitly
inductive use_bound {A : â„• â†’ â„•} : (â„• â†’. â„•) â†’ â„• â†’ â„• â†’ Prop
 | compu {g k} : Partrec g â†’ use_bound g k 0
 | self {k} : use_bound A k k.succ
 | pair {f:â„•â†’.â„•} {g:â„•â†’.â„•} {k uf ug} :
    use_bound f k uf â†’ use_bound g k ug â†’
    use_bound (fun n => pair <$> f n <*> g n) k (max uf ug)
 | comp {f:â„•â†’.â„•} {g:â„•â†’.â„•} {k uf ug} :
    (h : g k â‰  Part.none) â†’ use_bound f (g k|>.get <|PartENat.ne_top_iff_dom.mp h) uf â†’
      use_bound g k ug â†’
        use_bound (fun n => g n >>= f) k (max uf ug)
-- do this for `prec` and `rfind` and then prove that a use exists whenever f is @Partrec_in A
-- and both are total.

/-- âˆ€ B, B â‰¤â‚œ C â†’ (âˆ€ A, A â‰¤â‚œ B â†’ A â‰¤â‚œ C). -/
theorem computable_in_trans : Transitive Computable_in := fun X Y Z hXY hYZ =>
  @rec _ (fun B _ => âˆ€ A, A â‰¤â‚œ B â†’ A â‰¤â‚œ Z) (fun _ => id)
  (@rec _ _ zero zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind)
  (@rec _ _ succ zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind)
  (@rec _ _ left zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind)
  (@rec _ _ right zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind)
  (fun hâ‚€ hâ‚ _ _ => @rec _ _ (pair hâ‚€ hâ‚) zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind)
  (fun hâ‚€ hâ‚ _ _ => @rec _ _ (comp hâ‚€ hâ‚) zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind)
  (fun hâ‚€ hâ‚ _ _ => @rec _ _ (prec hâ‚€ hâ‚) zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind)
  (fun h _ => @rec _ _ (rfind h) zero succ left right
    (fun _ _ â†¦ pair) (fun _ _ â†¦ comp) (fun _ _ â†¦ prec) fun _ â†¦ rfind) Y hYZ X hXY

-- Now we can define Turing equivalence, deduce that it is an equiv.rel., and form the
-- ğ““áµ€.
example : Transitive T_reducible := by intro A B C hAB; exact computable_in_trans hAB

example : Reflexive T_reducible := by intro A; apply computable_in_refl

def T_equivalent (A B : â„• â†’ Bool) := T_reducible A B âˆ§ T_reducible B A

instance t_equiv_equiv : Equivalence T_equivalent := {
  refl := fun _ => âŸ¨self, selfâŸ©
  symm := by intro A B; exact fun h => âŸ¨h.2, h.1âŸ©
  trans := by
    intro A B C hâ‚€ hâ‚
    exact âŸ¨computable_in_trans hâ‚€.1 hâ‚.1, computable_in_trans hâ‚.2 hâ‚€.2âŸ©
}

instance ğ““_setoid : Setoid (â„• â†’ Bool) := {
  r := T_equivalent
  iseqv := t_equiv_equiv
}
def ğ““â‚œ := Quotient ğ““_setoid

/-- The Turing degree 0 contains all computable sets, but
 by definition it is just the Turing degree of âˆ…. -/
instance : Zero ğ““â‚œ := {
  zero := âŸ¦fun _ => falseâŸ§
}


def turing_reducible' (A B : â„• â†’. â„•) := @Partrec_in B A


instance blah : Encodable (Bool â†’ Bool) := {
  encode := by
    intro Ïƒ
    cases Ïƒ true
    Â· cases Ïƒ false
      Â· exact 0
      Â· exact 1
    Â· cases Ïƒ false
      Â· exact 2
      Â· exact 3
  decode := by
    intro n
    by_cases n < 4
    Â· by_cases n < 3
      Â· by_cases n < 2
        by_cases n < 1
        Â· exact some fun _ => false -- n=0
        Â· exact some fun x => !x -- n=1
        Â· exact some fun x => x -- n=2
      Â· exact some fun _ => true -- n=3
    Â· exact none
  encodek := by
    intro Ïƒ
    cases Ht : Ïƒ true
    Â· cases Hf : Ïƒ false
      Â· simp;ext x;cases x <;> tauto
      Â· simp;ext x;cases x <;> tauto
    Â· cases Hf : Ïƒ false
      Â· simp;ext x;cases x <;> tauto
      Â· simp;ext x;cases x <;> tauto
}



lemma encode_decode (k : â„•) :
    Encodable.encode (@Encodable.decode (Bool â†’ Bool) blah k) = ite (k < 4) k.succ 0 := by
  unfold blah
  simp_all
  split -- this is aesop output
  next h =>
    split
    next h_1 =>
      split
      next h_2 =>
        split
        next h_3 =>
          subst h_3
          simp_all only [ofNat_pos, Encodable.encode_some, succ_eq_add_one, zero_add]
        next h_3 =>
          simp_all only [Encodable.encode_some, succ_eq_add_one, add_left_inj]
          suffices k = 1 by
            subst this
            aesop
          omega
      next h_2 =>
        simp_all only [not_lt, Encodable.encode_some, succ_eq_add_one, reduceAdd, reduceEqDiff]
        exact Nat.eq_of_le_of_lt_succ h_2 h_1
    next h_1 =>
      simp_all only [not_lt, Encodable.encode_some, succ_eq_add_one, reduceAdd, reduceEqDiff]
      exact Nat.eq_of_le_of_lt_succ h_1 h
  next h => simp_all only [not_lt, Encodable.encode_none]

instance blahâ‚‚: Primcodable (Bool â†’ Bool) := {
  encode := by
    intro Ïƒ
    cases Ïƒ true
    Â· cases Ïƒ false
      Â· exact 0
      Â· exact 1
    Â· cases Ïƒ false
      Â· exact 2
      Â· exact 3
  decode := by
    intro n
    by_cases n < 4
    Â· by_cases n < 3
      Â· by_cases n < 2
        by_cases n < 1
        Â· exact some fun _ => false -- n=0
        Â· exact some fun x => !x -- n=1
        Â· exact some fun x => x -- n=2
      Â· exact some fun _ => true -- n=3
    Â· exact none
  encodek := by
    intro Ïƒ
    cases Ht : Ïƒ true
    Â· cases Hf : Ïƒ false
      Â· simp;ext x;cases x <;> tauto
      Â· simp;ext x;cases x <;> tauto
    Â· cases Hf : Ïƒ false
      Â· simp;ext x;cases x <;> tauto
      Â· simp;ext x;cases x <;> tauto
  prim := by
    apply Nat.Primrec.of_eq
    Â· show Nat.Primrec (fun k => ite (k < 4) k.succ 0)
      exact Primrec.nat_iff.mp <|Primrec.ite
        (PrimrecRel.comp Primrec.nat_lt Primrec.id <|Primrec.const 4) Primrec.succ <|Primrec.const 0
    Â· intro k
      have W := encode_decode k
      symm
      rw [W]
}

example : Primrec (fun (Ïƒ : Bool â†’ Bool) => Ïƒ true) := by
  apply Primrec.dom_fintype

example {n : â„•} : Primrec (fun (Ïƒ : Fin n.succ â†’ Bool) => Ïƒ 0) := by
  apply Primrec.dom_fintype




/-- A â‰¡â‚˜ B â†” A â‰¤â‚˜ B and B â‰¤â‚˜ A. -/
def m_equivalent {m : mon} (A B : â„• â†’ Bool) := @m_reducible m A B âˆ§ @m_reducible m B A


/-- A â‰¤â‚˜ B iff A is many-one reducible to B. -/
infix:50 " â‰¤â‚˜ " => m_reducible

/-- A â‰¡â‚˜ B iff A is many-one equivalent to B. -/
infix:50 " â‰¡â‚˜ " => m_equivalent


/-

## Basic properties of â‰¤â‚˜

-/

/-- m-reducibility is reflexive. -/
lemma m_refl {m : mon} : Reflexive (@m_reducible m ):=
  fun _ => âŸ¨ id, âŸ¨m.id, fun _ => rflâŸ©âŸ©

/-- m-reducibility is transitive. -/
lemma m_trans {m : mon} : Transitive (@m_reducible m) := by
  intro _ _ _ âŸ¨gâ‚,hgâ‚âŸ© âŸ¨gâ‚‚,hgâ‚‚âŸ©
  use gâ‚‚ âˆ˜ gâ‚
  constructor
  exact m.comp hgâ‚‚.1 hgâ‚.1
  intro x
  rw [hgâ‚.2 x, hgâ‚‚.2 (gâ‚ x)];rfl

/-- To do calc proofs with m-reducibility we create a Trans instance. -/
instance {m : mon} : Trans (@m_reducible m) (@m_reducible m) (@m_reducible m) := {
  trans := @m_trans m
}


/-

## Basic properties of â‰¡â‚˜

-/

/-- Many-one equivalence is reflexive. -/
lemma m_equiv_refl {m : mon} : Reflexive (@m_equivalent m) := fun _ => âŸ¨m_refl _, m_refl _âŸ©

/-- Many-one equivalence is transitive. -/
lemma m_equiv_trans {m : mon} : Transitive (@m_equivalent m) := by
  intro _ _ _ hâ‚ hâ‚‚
  unfold m_equivalent at *
  constructor
  exact m_trans hâ‚.1 hâ‚‚.1
  exact m_trans hâ‚‚.2 hâ‚.2

/-- Many-one equivalence is symmetric. -/
lemma m_equiv_symm {m : mon} : Symmetric (@m_equivalent m) := by
  intro A B h
  unfold m_equivalent at *
  constructor
  tauto;tauto

/-- Many-one equivalence. -/
lemma m_equiv_equiv {m : mon} : Equivalence (@m_equivalent m) :=
{
  refl := m_equiv_refl,
  symm := by have := @m_equiv_symm m; exact this,
  trans := by have := @m_equiv_trans m; exact this
}


/--

## The degree structure ğ““â‚˜, using quotients

`Quot` is like `Quotient` when the relation is not necessarily an equivalence.
We could do: def ğ““â‚˜' := Quot m_equivalent
-/
abbrev ğ““setoid {m : mon}: Setoid (â„• â†’ Bool) := {
  r := @m_equivalent m
  iseqv := m_equiv_equiv
}

/-- The many-one degrees. -/
abbrev ğ““ {m : mon} := @Quotient (â„• â†’ Bool) <|@ğ““setoid m

/-- Equivalent reals have equal upper cones. -/
lemma upper_cones_eq {m : mon} (A B : â„• â†’ Bool) (h : @m_equivalent m A B) :
    @m_reducible m A = @m_reducible m B :=
  Set.ext <| fun _ => Iff.intro (m_trans h.2) (m_trans h.1)

/-- Equivalent reals have equal degrees. -/
lemma degrees_eq {m : mon} (A B : â„• â†’ Bool) (h : @m_equivalent m A B) :
    @m_equivalent m A = @m_equivalent m B :=
  Set.ext <| fun _ => Iff.intro (m_equiv_trans (m_equiv_symm h)) (m_equiv_trans h)

/-- As an auxiliary notion, we define [A]â‚˜ â‰¤ b to mean
that the degree of A is below the degree b. -/
def le_m' {m : mon} (A : â„• â†’ Bool) (b : @ğ““ m) : Prop := by
  apply Quot.lift
  Â· intro C D
    intro (hCD : m_equivalent C D)
    show @m_reducible m A C = @m_reducible m A D
    exact eq_iff_iff.mpr <| Iff.intro (fun h => m_trans h hCD.1) fun h => m_trans h hCD.2
  Â· exact b

/-- The ordering of the m-degrees. -/
def le_m {m : mon} (a b : @ğ““ m) : Prop := by
  apply Quot.lift
  Â· intro C D
    intro (hCD : C â‰¡â‚˜ D)
    show le_m' C b = le_m' D b
    simp only [eq_iff_iff]
    unfold le_m'
    apply Eq.to_iff
    congr
    exact Set.ext fun A => âŸ¨m_trans hCD.2, m_trans hCD.1âŸ©
  Â· exact a

/-

## Basic properties of the m-degrees

-/

/-- The ordering of m-degrees is reflexive. -/
lemma le_m_refl {m : mon} : Reflexive <|@le_m m :=
  Quot.ind <| fun _ => m_refl _

/-- The ordering of m-degrees is transitive. -/
lemma le_m_trans {m : mon} : Transitive <|@le_m m :=
  Quot.ind fun _ => Quot.ind fun _ => Quot.ind fun _ h => m_trans h

/-- m-reducibility is a preorder. -/
def m_degrees_preorder {m : mon} : Preorder (â„• â†’ Bool) :=
  @Preorder.mk (â„• â†’ Bool) {le := @m_reducible m}
  {lt := fun A B => m_reducible A B âˆ§ Â¬ m_reducible B A}
    m_refl m_trans (by
      simp only;
      exact fun u v => by unfold m_reducible; trivial
    )

/-- For example ğ““â‚ is a partial order (if not a semilattice). -/
instance {m : mon}: PartialOrder <|@ğ““ m := {
  le := le_m
  le_refl := le_m_refl
  le_trans := le_m_trans
  le_antisymm := Quotient.ind <| fun A => Quotient.ind <| fun B hâ‚ hâ‚‚ => Quotient.sound âŸ¨hâ‚,hâ‚‚âŸ©
}

/-- The nontrivial computable sets form the m-degree `0`. -/
instance {m : mon} : Zero (@ğ““ m) := {
  zero := âŸ¦ (fun k => ite (k=0) true false) âŸ§
}

/-- The degree âŸ¦âˆ…âŸ§â‚˜ = âŠ¤. -/
instance {m : mon} : Bot (@ğ““ m) := {
  bot := âŸ¦ (fun _ => false) âŸ§
}

/-- The degree âŸ¦â„•âŸ§â‚˜ = âŠ¤. -/
instance {m : mon} : Top (@ğ““ m) := {
  top := âŸ¦ (fun _ => true) âŸ§
}

/--

  ## The recursive join A âŠ• B.

(However, the symbol âŠ• has a different meaning in Lean.)
It is really a shuffle or â™¯ (backslash sha).
-/
def join (A B : â„• â†’ Bool) := fun k => ite (Even k) (A (k/2)) <| B (k/2)

/-- Make sure â™¯ binds stronger than â‰¤â‚˜. -/
infix:70 " âŠ• " => join


/-- Join works as desired on the left. -/
lemma join_inl (A B : â„• â†’ Bool) (k : â„•): (join A B) (inlFun k) = A k := by
  unfold join inlFun
  simp

/-- Join works as desired on the right. -/
lemma join_inr (A B : â„• â†’ Bool) (k : â„•): (join A B) (inrFun k) = B k := by
  unfold join inrFun
  simp only [Nat.not_even_bit1, â†“reduceIte]
  congr
  omega


/-- A â‰¤â‚˜ A âŠ• B. -/
lemma join_left {m : monâ‚}  (A B : â„• â†’ Bool) : @m_reducible m.tomon A (A âŠ• B) :=
  âŸ¨fun k => 2 * k, m.inl, fun k => .symm <| join_inl A B kâŸ©

/-- B â‰¤â‚˜ A âŠ• B. -/
lemma join_right {m : monâ‚} (A B : â„• â†’ Bool) : @m_reducible m.tomon B (A âŠ• B) :=
  âŸ¨fun k => 2 * k + 1, m.inr, fun k => .symm <|join_inr A B kâŸ©




open Classical

/-- A map on ğ““â‚˜ that swaps âˆ… and â„•. -/
noncomputable def botSwap {m : mon} : @ğ““ m â†’ @ğ““ m := fun a =>
  ite (a = âŠ¥) âŠ¤ (ite (a = âŠ¤) âŠ¥ a)


/-- Swapping âˆ… and â„• is injective on ğ““â‚˜. -/
theorem botSwap_inj {m : mon} : Function.Injective <| @botSwap m := by
  intro a b h
  unfold botSwap at h
  split_ifs at h with gâ‚€ gâ‚ gâ‚‚ gâ‚ƒ gâ‚„ gâ‚…
  Â· exact Eq.trans gâ‚€ gâ‚.symm
  Â· exact False.elim <|(gâ‚‚ â–¸ gâ‚) h
  Â· exact False.elim <| gâ‚‚ h.symm
  Â· exfalso;apply gâ‚ƒ â–¸ h â–¸ gâ‚€;rfl
  Â· exact gâ‚ƒ â–¸ gâ‚….symm
  Â· exact False.elim <| gâ‚„ h.symm
  Â· exact False.elim <| gâ‚ƒ h
  Â· exact False.elim <| gâ‚€ h
  Â· exact h

/-- Swapping âˆ… and â„• is surjective on ğ““â‚˜. -/
theorem botSwap_surj {m : mon} : Function.Surjective <| @botSwap m := by
  Â· unfold botSwap
    intro b
    by_cases H : b = âŠ¥
    Â· subst H
      use âŠ¤
      simp
    Â· by_cases H : b = âŠ¤ <;> aesop

/-- In ğ““â‚˜, âŠ¥ is not below âŠ¤. -/
lemma emp_not_below {m : mon} : Â¬ (âŠ¥ : @ğ““ m) â‰¤ âŠ¤ := fun âŸ¨f,hfâŸ© => by simp at hf

/-- In ğ““â‚˜, âŠ¤ is not below âŠ¥. -/
lemma univ_not_below {m : mon} : Â¬ (âŠ¤ : @ğ““ m) â‰¤ âŠ¥ := fun âŸ¨f,hfâŸ© => by simp at hf

/-- In ğ““â‚˜, âŠ¥ is a minimal element. -/
theorem emp_min {m : mon} : âˆ€ (a : @ğ““ m), (h : a â‰¤ âŠ¥) â†’  a = âŠ¥ := by
  apply Quotient.ind
  intro A âŸ¨f,hfâŸ©

  unfold ğ““ ğ““setoid m_equivalent m_reducible at *
  simp_all only [Quotient.eq]
  apply Quot.sound
  have : A = fun _ => false := by ext x; exact hf.2 x
  constructor
  use f
  use f
  simp_all

/-- In ğ““â‚˜, âŠ¤ is a minimal element. -/
theorem univ_min {m : mon} : âˆ€ (a : @ğ““ m), (h : a â‰¤ âŠ¤) â†’  a = âŠ¤ := by
  apply Quotient.ind
  intro A âŸ¨f,hfâŸ©
  unfold ğ““ ğ““setoid m_equivalent m_reducible at *
  simp_all only [Quotient.eq]
  apply Quot.sound
  constructor
  use f
  use f
  simp_all

/-- An automorphism of a partial order is a bijection that preserves and reflects
the order. -/
def automorphism {Î± : Type} [PartialOrder Î±] (Ï€ : Î± â†’ Î±): Prop :=
  Function.Bijective Ï€ âˆ§ âˆ€ a b, a â‰¤ b â†” Ï€ a â‰¤ Ï€ b

/-- The complement map on `â„• â†’ Bool`. -/
def cpl : (â„• â†’ Bool) â†’ (â„• â†’ Bool) := fun A => (fun k => ! (A k))

/-- The complement map on ğ““â‚˜. -/
def complementMap {m : mon} : @ğ““ m â†’ @ğ““ m := by
  apply Quotient.lift
  intro A B âŸ¨âŸ¨fâ‚,hfâ‚âŸ©,âŸ¨fâ‚‚,hfâ‚‚âŸ©âŸ©
  show âŸ¦cpl AâŸ§ = âŸ¦cpl BâŸ§
  exact Quotient.sound <| âŸ¨âŸ¨fâ‚,hfâ‚.1, fun x => by unfold cpl; congr; exact hfâ‚.2 xâŸ©,
                           âŸ¨fâ‚‚,hfâ‚‚.1, fun x => by unfold cpl; congr; exact hfâ‚‚.2 xâŸ©âŸ©

/-- In ğ““â‚˜, âŠ¥ â‰  âŠ¤. -/
lemma emp_univ_m_degree {m : mon} : (âŠ¥ : @ğ““ m) â‰  âŠ¤ := by
  intro hc
  have : ğ““setoid.r (fun _ => false) (fun _ => true) := Quotient.eq''.mp hc
  unfold ğ““setoid m_equivalent at this
  simp only at this
  obtain âŸ¨f,hfâŸ© := this.1
  simp at hf

/-- The (âŠ¥,âŠ¤) swap map is not the identity. -/
theorem botSwapNontrivial {m : mon} : @botSwap m â‰  id := by
  intro hc
  have : âˆ€ a, @botSwap m a = id a := by exact fun a â†¦ congrFun hc a
  specialize this âŠ¥

  unfold botSwap at this
  simp_all only [ite_true, id_eq]
  apply emp_univ_m_degree.symm
  exact this

/-- A partial order is rigid if there are no nontrivial automorphisms. -/
def rigid (Î± : Type) [PartialOrder Î±] : Prop :=
  âˆ€ Ï€, @automorphism Î± _ Ï€ â†’ Ï€  = id

/-

## Computability results needed for monâ‚˜
-/

/-- Dividing-by-two is primitive recursive. -/
lemma half_primrec : Primrec (fun k => k/2) :=
  Primrec.of_graph
    âŸ¨id, âŸ¨Primrec.id, by
      intro x
      simp only [Encodable.encode_nat, id_eq]
      omega
    âŸ©âŸ©
    (PrimrecRel.compâ‚‚
      Primrec.eq
      (Primrecâ‚‚.compâ‚‚ Primrec.nat_div Primrecâ‚‚.left <| Primrecâ‚‚.const 2)
      Primrecâ‚‚.right)

/-- An arithmetical characterization of "Even" is primitive recursive. -/
lemma primrec_even_equiv : PrimrecPred fun k â†¦ k / 2 * 2 = k := by
    apply PrimrecRel.comp
    exact Primrec.eq
    apply Primrec.of_graph
    use id
    simp only [Encodable.encode_nat, id_eq]
    exact âŸ¨Primrec.id, fun x => by omegaâŸ©
    Â· exact (PrimrecRel.compâ‚‚ Primrec.eq
      (Primrecâ‚‚.compâ‚‚ Primrec.nat_mul
        (Primrecâ‚‚.compâ‚‚ Primrec.nat_div Primrecâ‚‚.left <| Primrecâ‚‚.const 2) <| Primrecâ‚‚.const 2)
        Primrecâ‚‚.right)
    Â· exact Primrec.id

/-- Characterizing "Even" arithmetically. -/
lemma even_div_two (a : â„•) : a / 2 * 2 = a â†” Even a :=
  Iff.intro (fun h => âŸ¨a / 2, Eq.trans h.symm (mul_two (a/2))âŸ©) <| Nat.div_two_mul_two_of_even

/-- "Even" is a primitive recursive predicate. -/
lemma even_primrec : @PrimrecPred â„• _ Even _ :=
  PrimrecPred.of_eq primrec_even_equiv even_div_two


/-- The usual join of functions on â„• is primitive recursive. -/
theorem primrec_join {fâ‚ fâ‚‚ : â„• â†’ â„•} (hfâ‚ : Primrec fâ‚) (hfâ‚‚ : Primrec fâ‚‚) :
    Primrec fun k â†¦ if Even k then fâ‚ (k / 2) else fâ‚‚ (k / 2) :=
  Primrec.of_eq
    (Primrec.cond (even_primrec)
      (Primrec.comp hfâ‚ <|half_primrec)
      (Primrec.comp hfâ‚‚ <|half_primrec))
    (by intro n; simp)


/-- The usual join of functions on â„• is computable. -/
theorem computable_join {fâ‚ fâ‚‚ : â„• â†’ â„•} (hfâ‚ : Computable fâ‚) (hfâ‚‚ : Computable fâ‚‚) :
    Computable fun k â†¦ if Even k then fâ‚ (k / 2) else fâ‚‚ (k / 2) :=
  Computable.of_eq
    (Computable.cond (Primrec.to_comp even_primrec)
      (Computable.comp hfâ‚ <|Primrec.to_comp half_primrec)
      (Computable.comp hfâ‚‚ <|Primrec.to_comp half_primrec))
    (by intro n; simp)

/-- An auxiliary lemma for proving that the join Aâ‚€ âŠ• Aâ‚ is monotone in Aâ‚€ within the context
 of the monoid class `monâ‚`.-/
lemma getHasIte {m : monâ‚} (hasIteâ‚‚ : âˆ€ {fâ‚ fâ‚‚}, m.func fâ‚ â†’ m.func fâ‚‚ â†’ m.func
    fun k â†¦ if Even k then fâ‚ (k / 2) else fâ‚‚ (k / 2)) :
    âˆ€ f, m.func f â†’ m.func (fun k : â„• => if Even k then f (k / 2) * 2 else k) := by
  intro f hf
  have : (fun k â†¦ if Even k then ((fun a => a * 2) âˆ˜ f) (k / 2) else
          (fun a => 2 * a + 1)  (k / 2))
        = fun k â†¦ if Even k then f (k / 2) * 2 else k := by
    ext k
    split_ifs with gâ‚€
    Â· rfl
    Â· show 2 * (k/2) + 1 = k
      have âŸ¨a,haâŸ© := odd_iff_exists_bit1.mp <| Nat.not_even_iff_odd.mp gâ‚€
      subst ha
      omega
  rw [â† this]
  exact @hasIteâ‚‚ ((fun a => a * 2) âˆ˜ f) (fun a => 2 * a + 1)
    (m.comp (by simp_rw [mul_comm _ 2]; exact m.inl) hf) m.inr

/-

## monâ‚˜ : a monoid that is a "clone" and closer to closure under primitive recursion.

-/

/-- Coding two functions into one. -/
def joinFun (fâ‚ fâ‚‚ : â„• â†’ â„•) := fun k â†¦ if Even k then fâ‚ (k / 2) else fâ‚‚ (k / 2)

/-- Requirement for a semilattice like ğ““â‚˜. -/
structure monâ‚˜ extends monâ‚ where
  join : âˆ€ {fâ‚ fâ‚‚}, func fâ‚ â†’ func fâ‚‚ â†’ func (joinFun fâ‚ fâ‚‚)
  const : âˆ€ c, func (fun _ => c)

structure monâ‚œâ‚œ extends monâ‚˜ where
  ttrefl : func fun n â†¦
    Encodable.encode
      ((Denumerable.ofNat ((k : â„•) Ã— (Fin k.succ â†’ Bool)) n).snd
        â†‘(Denumerable.ofNat ((k : â„•) Ã— (Fin k.succ â†’ Bool)) n).fst)

def tt_reducible (A B : â„• â†’ Bool) := âˆƒ u : â„• â†’ â„•, (Computable u âˆ§ Monotone u) âˆ§
  âˆƒ Î¦ : (n : â„•) â†’ (Fin (u n) â†’ Bool) â†’ Bool,
  Computable (fun pair : (n : â„•) Ã— (Fin (u n) â†’ Bool) => Î¦ pair.1 pair.2) âˆ§
    âˆ€ x, A x = Î¦ x (fun i => B i)

-- def tt_reducible_mon {m : monâ‚œâ‚œ} (A B : â„• â†’ Bool) := âˆƒ u : â„• â†’ â„•, (Computable u âˆ§ Monotone u) âˆ§
--   âˆƒ Î¦ : (n : â„•) â†’ (Fin (u n) â†’ Bool) â†’ Bool,
--   Computable (fun pair : (n : â„•) Ã— (Fin (u n) â†’ Bool) => Î¦ pair.1 pair.2) âˆ§
--     âˆ€ x, A x = Î¦ x (fun i => B i)


example {k : â„•} : Computable (fun (Ïƒ : Fin k.succ â†’ Bool) => Ïƒ 0) := by
  refine Primrec.to_comp ?hf
  apply Primrec.dom_fintype



/-- The computable functions satisfy the requirement for a semilattice like ğ““â‚˜. -/
def comput : monâ‚˜ := {
  func  := Computable
  id    := Computable.id
  comp  := Computable.comp
  inl   := Primrec.to_comp Primrec.nat_double
  inr   := Primrec.to_comp <| Primrec.nat_double_succ
  join  := computable_join
  const := Computable.const
}

/-- The primitive recursive functions satisfy the requirement for a semilattice like ğ““â‚˜. -/
def primrec : monâ‚˜ := {
  func := Primrec, id := Primrec.id, comp := Primrec.comp, inl := Primrec.nat_double,
  inr := Primrec.nat_double_succ, join := primrec_join, const := Primrec.const
}

/-- The all-monoid, which will give us only three degrees, âŠ¥, âŠ¤ and 0. -/
def allMon : monâ‚˜ := {
  func := fun _ => True, id := trivial, comp := fun _ _ => trivial,
  inl := trivial, inr := trivial, join := fun _ _ => trivial, const := fun _ => trivial
}

/-- The join Aâ‚€ âŠ• Aâ‚ is monotone in Aâ‚€. -/
theorem join_le_join {m : monâ‚˜} {Aâ‚€ Aâ‚ : â„• â†’ Bool} (h : @m_reducible m.tomon Aâ‚€ Aâ‚) (B : â„• â†’ Bool) :
    @m_reducible m.tomon (Aâ‚€ âŠ• B) (Aâ‚ âŠ• B) := by
  obtain âŸ¨f,hfâŸ© := h
  use fun k => ite (Even k) (f (k/2) * 2) k
  constructor
  Â· exact getHasIte m.join _ hf.1
  Â· intro k
    unfold join
    split_ifs with gâ‚€ gâ‚
    Â· rw [Nat.mul_div_left (f (k / 2)) Nat.zero_lt_two]
      exact hf.2 _
    Â· exact False.elim <| gâ‚ <| Nat.even_mul.mpr <| .inr <| Nat.even_iff.mpr rfl
    Â· rfl

/-- The join is bounded by each upper bound. -/
lemma join_le {m : monâ‚˜} {A B C : â„• â†’ Bool} (hâ‚ : @m_reducible m.tomon A C)
    (hâ‚‚ : @m_reducible m.tomon B C) : @m_reducible m.tomon (join A B) C := by
  obtain âŸ¨fâ‚,hfâ‚âŸ© := hâ‚
  obtain âŸ¨fâ‚‚,hfâ‚‚âŸ© := hâ‚‚
  use fun k => ite (Even k) (fâ‚ (k/2)) (fâ‚‚ (k/2))
  constructor
  Â· exact m.join hfâ‚.1 hfâ‚‚.1
  Â· intro k
    unfold join
    split_ifs with h
    exact hfâ‚.2 (k/2)
    exact hfâ‚‚.2 (k/2)


/-- The m-degree `[A]â‚˜ âŠ” b`. -/
def join' {m : monâ‚˜} (A : â„• â†’ Bool) (b : Quot <|@m_equivalent m.tomon) :
    Quot <|@m_equivalent m.tomon := by
  apply Quot.lift
  show âˆ€ B C, @m_equivalent m.tomon B C â†’
    Quot.mk m_equivalent (join A B) = Quot.mk m_equivalent (join A C)
  intro B C h;
  apply Quot.sound
  unfold m_equivalent at *
  constructor
  Â· apply join_le
    apply join_left
    calc
      B â‰¤â‚˜ C := h.1
      _ â‰¤â‚˜ _ := @join_right m.tomonâ‚ _ _
  Â· apply join_le
    apply join_left
    calc
      C â‰¤â‚˜ B := h.2
      _ â‰¤â‚˜ _ := @join_right m.tomonâ‚ _ _
  exact b



/-- ğ““â‚˜ is a join-semilattice. -/
instance {m : monâ‚˜}: SemilatticeSup <|@ğ““ m.tomon := {
  le := le_m
  le_refl := le_m_refl
  le_trans := le_m_trans
  le_antisymm := Quotient.ind <| fun A => Quotient.ind <| fun B hâ‚ hâ‚‚ => Quotient.sound âŸ¨hâ‚,hâ‚‚âŸ©

  le_sup_left  := Quotient.ind fun A => Quotient.ind fun B => by apply join_right
  le_sup_right := Quotient.ind fun A => Quotient.ind fun B => by apply join_left
  sup_le := Quotient.ind fun A => Quotient.ind fun B => Quotient.ind fun C hâ‚ hâ‚‚ => by
    exact join_le hâ‚‚ hâ‚
  sup := fun a => by
    apply Quotient.lift
    intro A B h
    show join' A a = join' B a
    unfold join'
    congr
    exact funext <| fun C => Quot.sound âŸ¨join_le_join h.1 C, join_le_join h.2 CâŸ©
}



/-- This is false for 1-degrees.
However, the complementing automorphism works there.
-/
theorem emp_univ {m : monâ‚˜} (B : â„• â†’ Bool) (h_2 : Â¬(âŸ¦BâŸ§ : @ğ““ m.tomon) = âŸ¦ (fun _ => false) âŸ§ ) :
    (âŸ¦ (fun _ => true) âŸ§ : @ğ““ m.tomon) â‰¤ âŸ¦BâŸ§ := by
  unfold ğ““setoid m_equivalent m_reducible at *
  by_cases H : B = (fun _ => false)
  Â· subst H
    exfalso
    apply h_2
    rfl
  Â· have âŸ¨k,hkâŸ© : âˆƒ k, B k â‰  false := by
      contrapose H
      simp_all only [ne_eq, Bool.not_eq_false, not_exists, Bool.not_eq_true, Decidable.not_not]
      ext x;tauto
    use fun _ => k
    simp_all only [ne_eq, Bool.not_eq_false, implies_true, and_true]
    exact m.const k

/-- In the m-degrees, if âŸ¦BâŸ§ â‰  âŠ¤ then âŠ¥ â‰¤ âŸ¦BâŸ§. -/
theorem univ_emp {m : monâ‚˜} (B : â„• â†’ Bool) (h_2 : âŸ¦BâŸ§ â‰  (âŠ¤ : @ğ““ m.tomon) ) :
    (âŠ¥ : @ğ““ m.tomon) â‰¤ âŸ¦BâŸ§ := by
  unfold ğ““ ğ““setoid m_equivalent m_reducible at *
  by_cases H : B = (fun _ => true)
  subst H
  exfalso
  apply h_2
  rfl
  have âŸ¨k,hkâŸ© : âˆƒ k, B k â‰  true := by
    contrapose H
    simp_all only [ne_eq, Bool.not_eq_true, not_exists, Bool.not_eq_false, Decidable.not_not]
    ext x;tauto
  use fun _ => k
  simp_all only [ne_eq, Bool.not_eq_true, implies_true, and_true]
  exact m.const k

/-- The complement map is not the identity map of ğ““â‚˜. -/
theorem complementMapIsNontrivial {m : mon} : @complementMap m â‰  id := by
  intro hc
  have : @complementMap m âŸ¦fun _ => falseâŸ§ = âŸ¦fun _ => falseâŸ§ := by rw [hc]; simp
  unfold complementMap cpl at this
  simp only [Quotient.lift_mk, Bool.not_false, Quotient.eq] at this
  obtain âŸ¨f,hfâŸ© := this.1
  simp at hf

/-- The complement map is a surjective map of ğ““â‚˜. -/
theorem complementMap_surjective {m : mon} : Function.Surjective <|@complementMap m := by
  unfold complementMap
  apply Quotient.ind
  intro A
  use âŸ¦ cpl A âŸ§
  simp only [Quotient.lift_mk, Quotient.eq]
  unfold cpl
  simp only [Bool.not_not]
  exact âŸ¨âŸ¨id, m.id, by tautoâŸ©, âŸ¨id, m.id, by tautoâŸ©âŸ©

/-- The complement map is an injective map of ğ““â‚˜. -/
theorem complementMap_injective {m : mon} : Function.Injective <|@complementMap m :=
  Quotient.ind fun A => Quotient.ind fun B h => Quotient.sound <| by
  unfold complementMap cpl at h
  simp only [Quotient.lift_mk, Quotient.eq] at h
  obtain âŸ¨âŸ¨fâ‚,hfâ‚âŸ©, âŸ¨fâ‚‚,hfâ‚‚âŸ©âŸ© := h
  simp only at hfâ‚ hfâ‚‚
  exact âŸ¨âŸ¨fâ‚, hfâ‚.1, fun x => by rw [â† Bool.not_not <| A x, â† Bool.not_not <| B <| fâ‚ x, hfâ‚.2 x]âŸ©,
         âŸ¨fâ‚‚, hfâ‚‚.1, fun x => by rw [â† Bool.not_not <| B x, â† Bool.not_not <| A <| fâ‚‚ x, hfâ‚‚.2 x]âŸ©âŸ©

/-- The complement map is an automorphism of ğ““â‚˜. -/
theorem complementMapIsAuto {m : mon} : (@automorphism (@ğ““ m)) complementMap :=
    âŸ¨âŸ¨complementMap_injective, complementMap_surjectiveâŸ©,
    Quotient.ind fun A => Quotient.ind fun B => by
      constructor
      Â· intro âŸ¨f,hfâŸ©
        use f
        unfold cpl
        tauto
      Â· exact fun âŸ¨f,hfâŸ© => âŸ¨f, hf.1, fun x => (Bool.not_not <| B <| f x) â–¸
          (Bool.not_not <| A <| x) â–¸ congrArg (fun b => !b) (hf.2 x)âŸ©âŸ©

/-- ğ““â‚˜ is not rigid. -/
theorem notrigid {m : mon} : Â¬ rigid (@ğ““ m) := by
  unfold rigid
  push_neg
  exact âŸ¨complementMap, complementMapIsAuto, complementMapIsNontrivialâŸ©


/-- Over a rich enough monoid, `botSwap` is an automorphism. -/
theorem botSwapIsAuto {m : monâ‚˜} : (@automorphism (@ğ““ m.tomon)) botSwap :=
  âŸ¨âŸ¨botSwap_inj, botSwap_surjâŸ©,
    Quotient.ind fun A => Quotient.ind fun B => by
      unfold botSwap
      split_ifs with gâ‚€ gâ‚ gâ‚‚ gâ‚ƒ gâ‚„ gâ‚… gâ‚† gâ‚‡
      Â· rw [gâ‚€,gâ‚];simp
      Â· rw [gâ‚€,gâ‚‚]
        exact âŸ¨fun h => False.elim <| emp_not_below h, fun h => False.elim <| univ_not_below hâŸ©
      Â· exact gâ‚€ â–¸ âŸ¨fun _ => emp_univ B gâ‚, fun _ => univ_emp B gâ‚‚âŸ©
      Â· rw [gâ‚ƒ,gâ‚„]
        exact âŸ¨fun h => False.elim <| univ_not_below h, fun h => False.elim <| emp_not_below hâŸ©
      Â· simp only [le_refl, iff_true];rw [gâ‚ƒ, gâ‚…];
      Â· rw [gâ‚ƒ]
        exact âŸ¨fun _ => univ_emp B gâ‚…, fun _ => emp_univ B gâ‚„âŸ©
      Â· rw [gâ‚†]
        exact âŸ¨fun h => False.elim <|  gâ‚€ <| emp_min âŸ¦AâŸ§ h,
              fun h => False.elim <|  gâ‚ƒ <| univ_min âŸ¦AâŸ§ hâŸ©
      Â· exact gâ‚‡ â–¸ âŸ¨fun h => False.elim <| gâ‚ƒ <| univ_min âŸ¦AâŸ§ h,
                    fun h => False.elim <| gâ‚€ <| emp_min âŸ¦AâŸ§ hâŸ©
      Â· tautoâŸ©


/-- In ğ““â‚˜, the degree of âˆ… is less than 0. -/
lemma emp_lt_zero {m : monâ‚˜} : âŠ¥ < (0 : @ğ““ m.tomon) := by
  refine lt_of_le_not_le ?_ ?_
  Â· use fun _ => 1
    simp only [one_ne_zero, â†“reduceIte, implies_true, and_true]
    exact m.const 1
  Â· intro âŸ¨f,hfâŸ©
    simp at hf

/-- âˆ… and â„• are the minimal elements of ğ““â‚˜. -/
lemma zero_one_m {E : monâ‚˜} {b : Bool} (A : â„• â†’ Bool) :
    A â‰  (fun _ => b) â†” @m_reducible E.tomon (fun _ => !b) A := by
  constructor
  Â· intro hA
    unfold m_reducible
    contrapose hA
    simp_all only [not_exists, not_and, not_forall, Bool.not_not_eq, ne_eq, Decidable.not_not]
    ext n
    have âŸ¨_,haâŸ© := hA (fun _ â†¦ n) (E.const _)
    exact ha.symm
  Â· intro âŸ¨g,hgâŸ© hc
    subst hc
    simp_all


open Classical

/-- The eth r.e. set -/
noncomputable def Ï† {e : Nat.Partrec.Code} : â„• â†’ Bool := fun n => (Nat.Partrec.Code.eval e n).Dom


/-- Defining the halting set K as {e | Ï†â‚‘(0)â†“}.
(There are other possible, essentially equivalent, definitions.) -/
noncomputable def K : â„• â†’ Bool := fun e =>
  (Nat.Partrec.Code.eval (Denumerable.ofNat Nat.Partrec.Code e) 0).Dom

/-- The halting set K is r.e. -/
theorem K_re : RePred fun k â†¦ (K k) = true := by
  unfold K
  have Q := ComputablePred.halting_problem_re 0
  simp_all only [decide_eq_true_eq]
  show RePred fun l => (fun c : Nat.Partrec.Code â†¦ (c.eval 0).Dom)
    ((fun k â†¦ Denumerable.ofNat Nat.Partrec.Code k) l)
  unfold RePred at *
  show Partrec fun l =>
    ( fun a : Nat.Partrec.Code â†¦ Part.assert
      ((fun c : Nat.Partrec.Code â†¦ (c.eval 0).Dom) a) fun _ â†¦ Part.some ())
    ((fun k â†¦ Denumerable.ofNat Nat.Partrec.Code k) l)
  let f := ( fun a : Nat.Partrec.Code â†¦ Part.assert
      ((fun c : Nat.Partrec.Code â†¦ (c.eval 0).Dom) a) fun _ â†¦ Part.some ())
  show Partrec fun l =>
    f
    ((fun k â†¦ Denumerable.ofNat Nat.Partrec.Code k) l)
  apply Partrec.comp
  exact Q
  exact Computable.ofNat Nat.Partrec.Code

/-- The complement of the halting set K is not r.e. -/
theorem Kbar_not_re : Â¬RePred fun k â†¦ (!K k) = true := by
  unfold K
  simp only [Bool.not_eq_true', decide_eq_false_iff_not]
  intro hc
  have hâ‚€ : (fun c : Nat.Partrec.Code â†¦ Â¬(c.eval 0).Dom)
           = fun c â†¦ Â¬((Denumerable.ofNat Nat.Partrec.Code (Encodable.encode c)).eval 0).Dom := by
    simp only [Denumerable.ofNat_encode]
  exact ComputablePred.halting_problem_not_re 0 <| hâ‚€ â–¸ Partrec.comp hc Computable.encode

/-- The complement of the halting set K is not computable. -/
theorem Kbar_not_computable : Â¬ Computable fun k => ! K k := by
  intro hc
  have : ComputablePred fun k â†¦ K k = false := by
    refine ComputablePred.computable_iff.mpr ?_
    use fun k => ! K k
    simp only [Bool.not_eq_true', and_true]
    exact hc
  exact Kbar_not_re <| ComputablePred.to_re (by simp_all)

/-- The halting set K is not computable. -/
theorem K_not_computable : Â¬ Computable K :=
  fun hc => Kbar_not_computable
    <| Computable.cond hc (Computable.const false) (Computable.const true)

def ğ““â‚˜ := @ğ““ comput.tomon

/-- If B is computable and A â‰¤â‚˜ B then A is computable. -/
theorem compute_closed_m_downward (A B : â„• â†’ Bool) (h : Computable B)
    (hâ‚€ : @m_reducible comput.tomon A B) : Computable A := by
  obtain âŸ¨f,hfâŸ© := hâ‚€
  have : A = B âˆ˜ f := by ext k; simp_all
  rw [this]
  apply Computable.comp h
  exact hf.1

/-- If B is r.e. and A â‰¤â‚˜ B then A is r.e. -/
theorem re_closed_m_downward {A B : â„• â†’ Bool} (h : RePred (fun (k : â„•) => (B k = true)))
    (hâ‚€ : @m_reducible comput.tomon A B) : RePred (fun (k : â„•) => (A k = true)) := by
  obtain âŸ¨f,hfâŸ© := hâ‚€
  have : A = B âˆ˜ f := by ext k; simp_all
  rw [this]
  unfold RePred at *
  simp_all only [Function.comp_apply, implies_true, and_true]
  exact Partrec.comp h hf

/-- The complement of K is not m-reducible to K. -/
theorem Kbar_not_below_K : Â¬ @m_reducible comput.tomon (fun k â†¦ (!K k) = true) K := by
  intro hc
  have : RePred (fun (k : â„•) => (! K k = true)) := re_closed_m_downward K_re (by simp_all)
  have := Kbar_not_re
  simp_all
